module statics
//imports signatures/chocopy-sig
imports lower


rules // single-file entry point

	programOk : Program
    
rules
  
  stmtOk: scope * Stmt
  stmtsOk maps stmtOk(*, list(*))

  ifBlkOk: scope * IfBlock

  elifBlkOk: scope * ElifBlock
  elifBlksOk maps elifBlkOk(*, list(*))

  elseBlkOk: scope * ElseBlock
  elseBlksOk maps elseBlkOk(*, list(*))
  
//Statements & Targets
  simpleStmtOk: scope * SimpleStmt
  targetOk: scope * Target -> TYPE
  blockOk: scope * Block

//Definitions
	defOk: scope * AllDefinitions
	defsOk maps defOk(*, list(*))

//Functions
	fnDefOk: scope * FunctionDef -> TYPE
	fnDefsOk maps fnDefOk(*, list(*)) = list(*)
	
	fnBodyOk: scope * FunctionBody
	fnDeclOk: scope * FunctionDeclarations
	fnDeclsOk maps fnDeclOk(*, list(*))

//Classes
	clsDefOk: scope * ClassDef -> TYPE
	clsBodyOk: scope * ClassBody
	
//Vars
	varDefOk: scope * VarDef -> TYPE
	varDefsOk maps varDefOk(*, list(*)) = list(*)

	declareVar: scope * TypedVar -> TYPE
	declareVarID: scope * ID * TYPE

	declareClass: scope * ID * TYPE * scope
	declareFunc: scope * ID * TYPE * list(TYPE)

	declGlobalLocalOk: scope * Declaration

	declareVarList: scope * TypedVarList -> list(TYPE)
	declareVars maps declareVar(*, list(*)) = list(*)

	typedVarToID: scope * TypedVar -> ID
	typeOfVar: scope * TypedVar -> TYPE

	typeOfVars maps typeOfVar(*, list(*)) = list(*)
	typeOfType: scope * Type -> TYPE

  
rules

////////////Syntax///////////////////////////////
	programOk(Program(defs, block)) :- {s}
		new s,
		s -P-> s,
		defsOk(s, defs),
		blockOk(s, block).

	programOk(ProgramOne(defs)) :- {s}
		new s, //TODO fix this
		s -P-> s,
		defsOk(s, defs).


	blockOk(s, Block(ls)) :-
		stmtsOk(s, ls).


		
////////////Statements///////////////////////////////
	stmtOk(s, SimpleStmt(simple)) :-
		simpleStmtOk(s, simple).

	stmtOk(s, For(id, expr, blk)) :- {T1}
		typeOfVarID(s, id) == T1 | error $[can not resolve [id]],
		exprOk(s, expr) == List(T1) | error $[[expr] neesds to be [List(T1)]],
		blockOk(s, blk).

	stmtOk(s, While(expr, blk)) :-
		exprOk(s, expr) == Bool(),
		blockOk(s, blk).
		
	///// If statmements ////////
	stmtOk(s, StmtIf(ifBlk, elifBlk, elseBlk)) :-
		ifBlkOk(s, ifBlk),
		elifBlksOk(s, elifBlk),
		elseBlksOk(s, elseBlk).
	
	ifBlkOk(s, IfBlock(expr, blk)) :-
		exprOk(s, expr) == Bool() | error $[[expr] needs to be a bool],
		blockOk(s, blk).

	elifBlkOk(s, ElifBlock(expr, blk)) :-
		exprOk(s, expr) == Bool() | error $[[expr] needs to be a bool],
		blockOk(s, blk).

	elseBlkOk(s, ElseBlock(blk)) :-
		blockOk(s, blk).
		
		
	
	simpleStmtOk(s, Pass()) :- true.

	simpleStmtOk(s, RetSimpleStmt(expr)) :- {T}
		exprOk(s, expr) == T.

	simpleStmtOk(s, Expression(expr)) :- {T}
		exprOk(s, expr) == T.

	simpleStmtOk(s, TargetSimpleStmt(trgt, exp)) :- {T1 T2}
		targetOk(s, trgt) == T1 | error $[[trgt] was expected to be declared, missing a declaration?],
		exprOk(s, exp) == T2,
		T1 == T2 | error $[The type of the target [T1] needs to match the type of the expr [T2]].

////////////Targets///////////////////////////////
/// Here we check vars have been declared
	targetOk(s, Identifier(x)) = T :- 
		typeOfVarID(s, x) == T | error $[can not find the declartion of identifier [x]].

	targetOk(s, TargetIndex(x)) = T :- {nameExpr indxExpr}
		indexExprOk(s, x) == T.

////TODO MemberExpr and IndexExpr



////////////VarDef///////////////////////////////

	typeOfType(s, e@TypeID("int")) = Int().
	typeOfType(s, e@TypeID("str")) = String().
	typeOfType(s, e@TypeID("bool")) = Bool().
	typeOfType(s, e@TypeID("object")) = Object().
	typeOfType(s, e@TypeID(x)) = ClassType(x).
	typeOfType(s, e@CollectionType(x)) = T :- {T1}
		typeOfType(s, x) == T1,
		T == List(T1),
		@e.type := List(T1).
		
	declareClass(s, id, T, s_class) :-
		!class[id, (T, s_class)] in s,
		resolveClass(s, id) == [(_,(_,_))] | error $[duplicate declarion for class [id]].
	

	declareFunc(s, id, FnType, FnArgsTypes) :-
		!func[id, (FnType, FnArgsTypes)] in s,
		resolveFunc(s, id) == [(_,(_,_))] | error $[duplicate declarion for function [id]].

	declareVarID(s, id, T) :-
		!var[id, T] in s,
		resolveVar(s, id) == [(_,(_,_))] | error $[duplicate declarion of [id]].
		
	declareVar(s, e@TypedVar(id, t)) = T :-
		typeOfType(s, t) == T | error $[can not find type of [t]],
		declareVarID(s, id, T),
		@e.type := T.
	
	declareVarList(s,TypedVarList(x)) = T  :- 
		declareVars(s, x) == T.

	declareVarList(s,TypedVarList([])) = T :-
		[] == T.
	
	//FIX
	declGlobalLocalOk(s, NonLocal(x)) :- true.


	//FIX
	declGlobalLocalOk(s, Global(x)) :- true.
		
		

	typeOfVar(s, TypedVar(x, t)) = T :- {x'}
		typeOfVarID(s, x) == T,
		@x.ref := x'.

		
	varDefOk(s, e@VarDef(tv, l)) = T :- {T1 T2}
		declareVar(s, tv) == T1,
		literalOk(s, l) == T2,
		sameTypes(T1, T2) | error $[the type of the var [T1] does not match the literal type [T2]],
		T1 == T,
		@e.type := T.

		
	defOk(s, e@VarDefinition(x)) :- {T}
		varDefOk(s, x) == T.
	
////////////Functions/////////////
	defOk(s, FunctionDefinition(x)) :- {T}
		fnDefOk(s, x) == T.
	
	fnDefOk(s, e@FnDef(x, listVar, retType, body)) = fnType :- {T s_fn argTypes}
		new s_fn,
		s_fn -P-> s_fn,
		typeOfType(s, retType) == T,
		declareVarList(s_fn, listVar) == argTypes | error $[declaration of arguments [listVar] in fuction is incorrect],
		fnBodyOk(s_fn, body),
		declareFunc(s, x, T, argTypes),
		FunType(T, argTypes) == fnType,
		@e.type := fnType.

	fnDefOk(s, e@FnDefNoType(x, listVar, body)) = fnType :- {s_fn argTypes}
		new s_fn,
		s_fn -P-> s_fn,
		declareVarList(s_fn, listVar) == argTypes | error $[declaration of arguments [listVar] in fuction is incorrect],
		fnBodyOk(s_fn, body),
		declareFunc(s, x, NoneType(), argTypes),
		FunType(NoneType(), argTypes) == fnType,
		@e.type := fnType.
	
	fnBodyOk(s, FnBody(decls, blk)) :-
		fnDeclsOk(s, decls),
		blockOk(s, blk).
	
	fnDeclOk(s, FnVarDef(x))  :- {T}
		varDefOk(s, x) == T.

	fnDeclOk(s, FnFnDef(x))  :- {T}
		fnDefOk(s, x) == T.
	
	fnDeclOk(s, FnLocalGlobals(x))  :- {T}
		declGlobalLocalOk(s, x).
		
////////////Functions/////////////
	defOk(s, ClassDefinition(x)) :- {T}
		clsDefOk(s, x) == T.
	
	clsDefOk(s, e@ClsDef(clsId, parentId, body)) = T :-  {T2 s_cls}
		new s_cls,
		s_cls -P-> s_cls,
		typeOfClassID(s, parentId) == T2, //type of parent
		validClassType(T2) | error $[parent type [T2] is not valid],
		clsBodyOk(s_cls, body) | error $[body class is not ok],
		ClassType(clsId) == T,
		declareClass(s, clsId, T, s_cls),
		@e.type := T.
	
	clsBodyOk(s, ClsBodyEmpty()) :- true.
	clsBodyOk(s, ClsBody(varDefs, fnDefs)) :-  {T1 T2}
		varDefsOk(s, varDefs) == T1 | error $[var defs not ok in class],
		fnDefsOk(s, fnDefs) == T2 | error $[functions defs not ok in class].
		
		

		

		
		


		

